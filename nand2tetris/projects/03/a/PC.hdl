// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    //check inc option
    Inc16(in=c, out=i);
    Mux16(a=c, b=i, sel=inc, out=step1);

    //check load option
    Mux16(a=step1, b=in, sel=load, out=step2);

    //check reset option
    Mux16(a=step2, b=false, sel=reset, out=step3);

    //Check if we should load
    Or(a=load, b=inc, out=li);
    Or(a=li, b=reset, out=loadnew);

    Register(in=step3, load=loadnew, out=c, out=out);
}
